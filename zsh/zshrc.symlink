#!/bin/zsh

export DOTFILES="$HOME/.dotfiles"

export EDITOR="vim"

export LC_ALL="en_US.UTF-8"
export LANG="en_US"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# load antibody plugins
source ~/.zsh_plugins.sh

export ZSH=$HOME/.oh-my-zsh

# fix zsh-syntax-highlighting bug w/ antibody
unset X_ZSH_HIGHLIGHT_DIRS_BLACKLIST

plugins=(git terraform)

ZSH_THEME="hyperzsh"

# disable magic functions - magic functions slow down pasting
# https://apple.stackexchange.com/questions/312795/zsh-paste-from-the-clipboard-a-command-takes-a-few-second-to-be-write-in-the-ter
# https://github.com/ohmyzsh/ohmyzsh/issues/5569
DISABLE_MAGIC_FUNCTIONS=true

source $ZSH/oh-my-zsh.sh

# load everything but the path and completion files
for file in ${config_files:#*/path.zsh}; do
  source "$file"
done

autoload -Uz compinit
for dump in ~/.zcompdump(N.mh+24); do
  compinit
done
compinit -C

unset config_files updated_at

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc
